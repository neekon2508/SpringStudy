import org.springframework.boot.gradle.plugin.SpringBootPlugin

description 'Chapter 02: Hello World!'


// we are using Spring Boot dependency management,
// but we configure Spring MVC in the classic manner
dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    implementation 'org.springframework:spring-context'
    implementation "org.codehaus.groovy:groovy-all:$groovyVersion"
}

tasks.register('fatJar', Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    /*  Gradle Fat jars with multiple dependencies are affected by the same bug: https://issues.apache.org/jira/browse/MASSEMBLY-360 this statement fixes it */
    manifest {
        attributes "Main-Class": "com.apress.prospring6.two.HelloWorldSpringDI",
                "Created-By": "Iuliana Cosmina",
                "Specification-Title": "Pro Spring 6 - Chapter 2"
    }
    from {configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

